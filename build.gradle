buildscript {
    repositories {
        jcenter()

        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'http://files.minecraftforge.net/maven' }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven-publish'

if (System.getenv("BUILD_NUMBER") != null) {
    version = "${minecraft_version}-${mod_version}+build-${System.getenv("BUILD_NUMBER")}"
} else {
    version = "${minecraft_version}-${mod_version}+snapshot-${System.currentTimeMillis()}"
}

group = "me.jellysquid.mods"
archivesBaseName = "${mod_name}"

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"

    mappings = forge_mappings
}

minecraft {
    clientJvmArgs += "-Dfml.coreMods.load=me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin"
    serverJvmArgs += "-Dfml.coreMods.load=me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin"
}

repositories {
    maven { url = 'https://repo.spongepowered.org/maven' }
}

configurations {
    shade

    compile.extendsFrom shade
}

dependencies {
    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        transitive = false
    }
}

jar {
    from sourceSets.main.output

    manifest {
        attributes 'Maven-Artifact': "${group}:${archivesBaseName}:${version}"
    }

    manifest.mainAttributes(
            "FMLAT": "phosphor_at.cfg",
            "FMLCorePlugin": "me.jellysquid.mods.phosphor.core.PhosphorFMLLoadingPlugin",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": 0,
            "FMLCorePluginContainsFMLMod": true,
            "ForceLoadAsMod": true
    )

    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            rename 'LICENSE.txt', 'org/spongepowered/LICENSE.txt'

            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.RSA'
            exclude 'META-INF/*.MF'
        }
    }

    classifier = 'universal'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


mixin {
    add sourceSets.main, "mixins.phosphor.refmap.json"
}

minecraft {
    makeObfSourceJar = true
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    keyStore = System.getenv('SIGN_KEYSTORE')
    alias = System.getenv('SIGN_ALIAS')
    storePass = System.getenv('SIGN_STORE_PASS')
    keyPass = System.getenv('SIGN_KEY_PASS')

    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

if (System.getenv("SIGN_BUILD") == 'true') {
    build.dependsOn signJar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'

    from sourceSets.main.allSource
}


task deobfJar(type: Jar, dependsOn: classes) {
    classifier = 'deobf'

    from sourceSets.main.output
}

publishing {
    publications {
        mod(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact deobfJar

            pom {
                version = "${minecraft_version}-${mod_version}-SNAPSHOT"
            }
        }
    }

    repositories {
        def mavenSecretsPath = System.getenv("MAVEN_SECRETS_FILE")

        if (mavenSecretsPath != null) {
            def mavenSecretsFile = file(mavenSecretsPath).newReader()
            def mavenSecrets = new Properties()
            mavenSecrets.load(mavenSecretsFile)

            maven {
                url = System.getenv("MAVEN_PUBLISH_URL")

                credentials {
                    username = System.getenv("MAVEN_PUBLISH_USERNAME")
                    password = System.getenv("MAVEN_PUBLISH_PASSWORD")
                }
            }
        }
    }


}
